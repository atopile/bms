from "esp32-s3/elec/src/esp32-s3.ato" import ESP32S3
from "isoSPIConnector.ato" import isoSPIConnector
from "ADBMS6822.ato" import ADBMS6822Module
from "HM2102NLT.ato" import HM2102NLT
from "tps63020dsjr/elec/src/tps63020dsjr.ato" import TPS63020DSJR
from "jst_2p.ato" import JST_PH_2P
from "generics/interfaces.ato" import Power, SPI
from "usb-connectors/usb-connectors.ato" import USBCConn
from "generics/leds.ato" import LEDIndicatorRed
from "_1N5819WS.ato" import _1N5819WS
from "AO3401A.ato" import AO3401A
from "generics/resistors.ato" import Resistor
from "generics/capacitors.ato" import Capacitor
from "bq24045dsqr/elec/src/bq24045dsqr.ato" import BQ24040DSQR
from "tf-01a/elec/src/tf-01a.ato" import TF_01A
from "programming-headers/elec/src/programming-headers.ato" import JTAG_10PIN
from "sk6805-ec20/elec/src/sk6805-ec20.ato" import SK6805EC20


module PowerPrioritiser:
    """Prioritise the power rails"""
    signal _gnd
    high_priority = new Power
    low_priority = new Power
    out = new Power

    # Tie all the grounds
    _gnd ~ high_priority.gnd
    _gnd ~ low_priority.gnd
    _gnd ~ out.gnd

    diode = new _1N5819WS
    high_priority.vcc ~ diode.anode; diode.cathode ~ out.vcc

    pfet = new AO3401A
    low_priority.vcc ~ pfet.drain; pfet.source ~ out.vcc

    # Tie the gate hard up to the high priority rail
    # and soft down to ground
    _pulldown = new Resistor
    _pulldown.value = 10kΩ to 47kΩ
    high_priority.vcc ~ pfet.gate; pfet.gate ~ _pulldown.p1; _pulldown.p2 ~ _gnd


module ADICommunicator:
    """
    This is a development, debugging and validation tool for use with isoSPI BMS devices.
    """
    uc = new ESP32S3
    jtag = new JTAG_10PIN
    uc.jtag ~ jtag.jtag

    ## Power
    usb_conn = new USBCConn
    usb_conn.usb2 ~ uc.usb2
    usb_conn.usb2.dp.override_net_name = "usb+"
    usb_conn.usb2.dm.override_net_name = "usb-"

    battery_conn = new JST_PH_2P
    power_batt = new Power
    battery_conn.vcc ~ power_batt.vcc
    battery_conn.gnd ~ power_batt.gnd

    # Ideally draw from the USB connection
    # and prevent back-feeding the battery
    power_prio = new PowerPrioritiser
    usb_conn.power ~ power_prio.high_priority
    power_batt ~ power_prio.low_priority

    # Power rails
    _5v_reg = new TPS63020DSJR
    power_prio.out ~ _5v_reg.power_in
    _5v_reg.v_in = 2.5V to 4.2V
    _5v_reg.v_out = 5V +/- 10%
    power_5v = new Power
    _5v_reg.power_out ~ power_5v

    _3v3_reg = new TPS63020DSJR
    power_prio.out ~ _3v3_reg.power_in
    _3v3_reg.v_in = 2.5V to 4.2V
    _3v3_reg.v_out = 3.3V +/- 10%
    power_3v3 = new Power
    _3v3_reg.power_out ~ power_3v3
    power_3v3 ~ uc.power

    power_indicator = new LEDIndicatorRed
    power_3v3 ~ power_indicator.power
    power_indicator.v_in = _3v3_reg.v_out

    # Battery charger
    _charger = new BQ24040DSQR
    usb_conn.power ~ _charger.power_in
    _charger.power_batt ~ power_batt

    ## SPI Network things
    _nic = new ADBMS6822Module
    power_3v3 ~ _nic.logic_power
    power_5v ~ _nic.iso_power

    # Isolated SPI transformer
    _iso_transformer = new HM2102NLT
    _nic.isospi_1 ~ _iso_transformer.logic_1
    _nic.isospi_2 ~ _iso_transformer.logic_2
    _nic.isospi_1.p.override_net_name = "isospi_safe_1+"
    _nic.isospi_1.n.override_net_name = "isospi_safe_1-"
    _nic.isospi_2.p.override_net_name = "isospi_safe_2+"
    _nic.isospi_2.n.override_net_name = "isospi_safe_2-"

    _iso_cap_1 = new Capacitor
    _iso_cap_2 = new Capacitor
    _iso_cap_1.value = 10nF +/- 20%
    _iso_cap_2.value = 10nF +/- 20%
    _iso_transformer.logic_1_center ~ _iso_cap_1.p1; _iso_cap_1.p2 ~ power_3v3.gnd
    _iso_transformer.logic_2_center ~ _iso_cap_2.p1; _iso_cap_2.p2 ~ power_3v3.gnd

    bms_1_conn = new isoSPIConnector
    bms_1_conn.isospi ~ _iso_transformer.iso_1
    bms_2_conn = new isoSPIConnector
    bms_2_conn.isospi ~ _iso_transformer.iso_2
    bms_1_conn.isospi.p.override_net_name = "isospi_hv_1+"
    bms_1_conn.isospi.n.override_net_name = "isospi_hv_1-"
    bms_2_conn.isospi.p.override_net_name = "isospi_hv_2+"
    bms_2_conn.isospi.n.override_net_name = "isospi_hv_2-"

    _nic.logic_spi_1.miso ~ _nic.logic_spi_2.miso
    _nic.logic_spi_1.mosi ~ _nic.logic_spi_2.mosi
    _nic.logic_spi_1.sck ~ _nic.logic_spi_2.sck
    # FIXME: This is a hack to get the CS lines to be different
    uc.io9 ~ _nic.logic_spi_1.mosi
    uc.io46 ~ _nic.logic_spi_1.miso
    uc.io3 ~ _nic.logic_spi_1.sck
    uc.io18 ~ _nic.logic_spi_1.cs
    uc.io8 ~ _nic.logic_spi_2.cs

    ## SD Card on Fast SPI interface (SPI2)
    _sd_card = new TF_01A
    uc.spi2 ~ _sd_card.spi
    power_3v3 ~ _sd_card.power

    ## General-Purpose LED indicator
    _led = new SK6805EC20
    uc.io1 ~ _led.din.io
    power_3v3 ~ _led.power
